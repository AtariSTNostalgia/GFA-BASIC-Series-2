'
' *** Main code ***
'
@main
'
' *** Procedures ***
'
> PROCEDURE main
  @setvalues
  @background
  @bounce
RETURN
> PROCEDURE setvalues
  CLS
  INPUT "Enter desired border size: ",b&
  xmin&=b&
  ymin&=b&
  xmax&=639-b&
  ymax&=399-b&
  x1&=b&+RANDOM(639-2*b&)
  y1&=b&+RANDOM(399-2*b&)
  drx1&=1
  dry1&=1
  x2&=b&+RANDOM(639-2*b&)          ! Same stuff for extra coordinates
  y2&=b&+RANDOM(399-2*b&)
  drx2&=-1
  dry2&=-1
  CLS
RETURN
> PROCEDURE background
  DEFFILL 1,4,0
  PBOX 0,0,639,399
  DEFFILL 1,0,0
  PBOX b&-1,b&-1,640-b&,400-b&
RETURN
> PROCEDURE bounce
  GRAPHMODE 1
  DO UNTIL ASC(INKEY$)=32
    WHILE x1&<=xmax& AND x2&<=xmax& AND x1&>=xmin& AND x2&>=xmin& AND y1&<=ymax& AND y2&<=ymax& AND y1&>=ymin& AND y2&>=ymin&
      PLOT x1&,y1&
      PLOT x2&,y2&             ! Add extra coordinates here too
      ADD x1&,drx1&
      ADD y1&,dry1&
      ADD x2&,drx2&
      ADD y2&,dry2&
    WEND                       ! The WHILE check is bigger, but still 1 line
    IF x1&>xmax& OR x1&<xmin&
      MUL drx1&,-1
      ADD x1&,2*drx1&
    ENDIF
    IF y1&>ymax& OR y1&<ymin&
      MUL dry1&,-1
      ADD y1&,2*dry1&
    ENDIF
    IF x2&>xmax& OR x2&<xmin&  ! Repeat actions for second coordinate set
      MUL drx2&,-1
      ADD x2&,2*drx2&
    ENDIF
    IF y2&>ymax& OR y2&<ymin&
      MUL dry2&,-1
      ADD y2&,2*dry2&
    ENDIF
  LOOP
  @quit
RETURN
> PROCEDURE quit
  ALERT 3,"|     Stop or go again?     ",1,"Stop|Go again",quit|
  SELECT quit|
  CASE 1
    END
  CASE 2
    @main
  ENDSELECT
RETURN
